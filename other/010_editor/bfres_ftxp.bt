// ==== Structures =====================================================================================================

typedef struct // TexPatternAnim
{
	char signature[4]; // FTXP
	NameOffset ofsName;
	NameOffset ofsPath;
	enum <ushort> TexPatternAnimFlags
	{
		TAF_None,
		TAF_CurveBaked = 1 << 0,
		TAF_IsLooping = 1 << 2
	} flg : 2;
	ushort numUserData;
	int numFrame;
	ushort numTextureRef;
	ushort numMatAnim;
	uint numPatAnim;
	uint numCurve;
	uint sizBaked;
	Offset ofsModel; // Points to FMDL
	Offset ofsBindIndexList;
	Offset ofsMatAnimList;
	Offset ofsTextureRefDict;
	Offset ofsUserDataDict;
	// Read bind indices.
	FSeek(ofsBindIndexList.target);
	ushort bindIndices[numMatAnim] <optimize=true>;
	FAlign(4);
	// Read material animations.
	FSeek(ofsMatAnimList.target);
	TexPatternMatAnim matAnims[numMatAnim] <optimize=false>;
	// Read texture references.
	FSeek(ofsTextureRefDict.target);
	Dict textureRefs(DT_TEXTURE_REF);
	// Read user data.
	if (ofsUserDataDict.value)
	{
		FSeek(ofsUserDataDict.target);
		Dict userData(DT_USER_DATA);
	}
} TexPatternAnim <bgcolor=0xFFEFCD, read=TexPatternAnimRead>;

typedef struct // TexPatternMatAnim
{
	ushort numPatAnim;
	ushort numCurve;
	uint beginCurve;
	uint beginPatAnim;
	NameOffset ofsName;
	Offset ofsPatAnimInfoList;
	Offset ofsCurveList;
	Offset ofsBaseValue;
	local uint pos <hidden=true> = FTell();
	// Read pattern animation infos.
	FSeek(ofsPatAnimInfoList.target);
	PatAnimInfo patAnimInfos[numPatAnim] <optimize=false>;
	// Read animation curves.
	if (numCurve)
	{
		FSeek(ofsCurveList.target);
		AnimCurve animCurves[numCurve] <optimize=false>;
	}
	// Read base values.
	FSeek(ofsBaseValue.target);
	ushort baseValues[numPatAnim] <bgcolor=0xC8BBA1, optimize=true>;
	FAlign(4);
	FSeek(pos); // Seek back to header end.
} TexPatternMatAnim <bgcolor=0xEBDCBD, read=TexPatternMatAnimRead>;

typedef struct // PatAnimInfo
{
	byte idxCurve;
	byte idxSubBind;
	ushort padding <fgcolor=0x808080, hidden=true>;
	NameOffset ofsName;
} PatAnimInfo <bgcolor=0xD9CBAE, read=PatAnimInfoRead>;

// ==== Template Methods ===============================================================================================

string PatAnimInfoRead(PatAnimInfo &patAnimInfo)
{
	return patAnimInfo.ofsName.name;
}

string TexPatternAnimRead(TexPatternAnim &texPatternAnim)
{
	return texPatternAnim.ofsName.name;
}

string TexPatternMatAnimRead(TexPatternMatAnim &texPatternMatAnim)
{
	return texPatternMatAnim.ofsName.name;
}
